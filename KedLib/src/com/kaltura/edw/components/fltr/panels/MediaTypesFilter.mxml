<?xml version="1.0" encoding="utf-8"?>
<panels:AdditionalFilter xmlns:mx="http://www.adobe.com/2006/mxml" 
						 xmlns:panels="com.borhan.edw.components.fltr.panels.*"
						 creationComplete="creationCompleteHandler(event)">
	<mx:Script>
		<![CDATA[
			import com.borhan.types.BorhanMediaType;
			
			import mx.events.FlexEvent;
			
			[Bindable]
			public var showLiveStream:Boolean = true;
			
			/**
			 * Creates an object with keys as media types,
			 * later used for easy access while parsing this data.
			 * The optional keys are enumerated in <code>BorhanMediaType</code>
			 * @param mediaTypes	the string that represents media
			 * 						types on a filter
			 * @return an object like {1:true, 201:true, ..}
			 * */
			protected function mediaTypesToObject(mediaTypes:String = ''):Object {
				var result:Object = {};
				if (mediaTypes) {
					var mediaTypesArray:Array = mediaTypes.split(",");
					for (var i:int = 0; i < mediaTypesArray.length; i++) {
						result[mediaTypesArray[i]] = true;
					}
				}
				return result;
			}
			

			override public function set filter(value:Object):void {
				// value is a list of BorhanMediaType-s separated by ",".
				var mediaTypes:Object  = mediaTypesToObject(value.toString()) ;
				if (!value || (mediaTypes[BorhanMediaType.VIDEO] && mediaTypes[6] && mediaTypes[BorhanMediaType.AUDIO] && mediaTypes[BorhanMediaType.LIVE_STREAM_FLASH] && mediaTypes[BorhanMediaType.IMAGE])) {
					// all types selected
					selectAllTypesBtn.selected = true;
					mixType.selected = false;
					audioType.selected = false;
					videoType.selected = false;
					imageType.selected = false;
					imageType.selected = false;
					streamType.selected = false;
				}
				else {
					selectAllTypesBtn.selected = false;
					
					if (mediaTypes[BorhanMediaType.VIDEO])
						videoType.selected = true;
					if (mediaTypes[6])
						mixType.selected = true;
					if (mediaTypes[BorhanMediaType.AUDIO])
						audioType.selected = true;
					if (mediaTypes[BorhanMediaType.IMAGE])
						imageType.selected = true;
					if (mediaTypes[BorhanMediaType.LIVE_STREAM_FLASH])
						streamType.selected = true;
				}
			}
			
			override public function get filter():Object {
				var mediaTypeString:String = '';
				//if the select all media types is selected - add all media types
				if (selectAllTypesBtn.selected) {
					mediaTypeString = BorhanMediaType.VIDEO + "," + BorhanMediaType.IMAGE + "," + BorhanMediaType.AUDIO + ",6," + BorhanMediaType.LIVE_STREAM_FLASH;
				}
				else {
					if (videoType.selected)
						mediaTypeString += BorhanMediaType.VIDEO + ",";
					if (imageType.selected)
						mediaTypeString += BorhanMediaType.IMAGE + ",";
					if (audioType.selected)
						mediaTypeString += BorhanMediaType.AUDIO + ",";
					if (streamType.selected)
						mediaTypeString += BorhanMediaType.LIVE_STREAM_FLASH + ",";
					if (mixType.selected) {
						// mix is not in the enum, so it's hardcoded
						mediaTypeString += "6,";
					}
					//remove the last ,				 
					mediaTypeString = mediaTypeString.slice(0, mediaTypeString.length - 1);
				}
				return mediaTypeString;
			}

			protected function creationCompleteHandler(event:FlexEvent):void {
				_buttons = [selectAllTypesBtn, videoType, imageType, audioType, mixType, streamType];
				friendlyName = resourceManager.getString('filter', 'mediaTypeTooltip');
			}

		]]>
	</mx:Script>
	
	<mx:CheckBox id="selectAllTypesBtn" width="100%" selected="true"
			   label="{resourceManager.getString('filter', 'allMediaTypes')}"
			   toolTip="{resourceManager.getString('filter', 'allMediaTypes')}"
			   click="onDynamicTitleClicked(event)" styleName="mainFilterGroupButton"/>
	<mx:CheckBox id="videoType" width="100%" selected="false"
				 label="{resourceManager.getString('filter', 'video')}" 
				 toolTip="{resourceManager.getString('filter', 'video')}" 
				 click="onDynamicMemberClicked(event)" styleName="innerFilterGroupButton"/>
	<mx:CheckBox id="imageType" width="100%" selected="false"
				 label="{resourceManager.getString('filter', 'image')}" 
				 toolTip="{resourceManager.getString('filter', 'image')}" 
				 click="onDynamicMemberClicked(event)" styleName="innerFilterGroupButton" />
	<mx:CheckBox id="audioType" width="100%" selected="false"
				 label="{resourceManager.getString('filter', 'audio')}" 
				 toolTip="{resourceManager.getString('filter', 'audio')}" 
				 click="onDynamicMemberClicked(event)" styleName="innerFilterGroupButton" />
	<mx:CheckBox id="mixType" width="100%" selected="false"
				 label="{resourceManager.getString('filter', 'videoMix')}" 
				 toolTip="{resourceManager.getString('filter', 'videoMix')}" 
				 click="onDynamicMemberClicked(event)" styleName="innerFilterGroupButton" />
	<mx:CheckBox id="streamType" width="100%" selected="false"
				 label="{resourceManager.getString('filter', 'liveStream')}" 
				 toolTip="{resourceManager.getString('filter', 'liveStream')}" 
				 visible="{showLiveStream}" includeInLayout="{showLiveStream}"
				 click="onDynamicMemberClicked(event)" styleName="innerFilterGroupButton" />
</panels:AdditionalFilter>
