<?xml version="1.0" encoding="utf-8"?>
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%" verticalAlign="middle" horizontalAlign="left" paddingLeft="6">
	<mx:Script>
		<![CDATA[
			import com.borhan.edw.constants.DistributionActionType;
			import com.borhan.edw.model.EntryDistributionWithProfile;
			import com.borhan.types.BorhanEntryDistributionFlag;
			import com.borhan.types.BorhanEntryDistributionStatus;
			import com.borhan.vo.BorhanEntryDistribution;
			
			private static const DISTRIBUTE:String = "distribute";
			private static const RETRY:String = "retry";
			private static const UPDATE:String = "update";
			
			private var _currentAction:String;
			private var _entryDis:BorhanEntryDistribution;
			
			override public function set data(value:Object):void {
				super.data = value;
				_entryDis = (value as EntryDistributionWithProfile).borhanEntryDistribution;
				var action:String;

				if (!_entryDis.validationErrors || _entryDis.validationErrors.length == 0) {
					switch (_entryDis.status) {
						case BorhanEntryDistributionStatus.PENDING:
							if (_entryDis.dirtyStatus != BorhanEntryDistributionFlag.SUBMIT_REQUIRED)
								action = DISTRIBUTE;
							break;
						
						case BorhanEntryDistributionStatus.ERROR_DELETING:
						case BorhanEntryDistributionStatus.ERROR_SUBMITTING:
						case BorhanEntryDistributionStatus.ERROR_UPDATING:
							action = RETRY;
							break;
						case BorhanEntryDistributionStatus.READY:
							if (_entryDis.dirtyStatus==BorhanEntryDistributionFlag.UPDATE_REQUIRED)
								action = UPDATE;
							break;
						case BorhanEntryDistributionStatus.DELETING:
						case BorhanEntryDistributionStatus.SUBMITTING:
						case BorhanEntryDistributionStatus.UPDATING:
						case BorhanEntryDistributionStatus.QUEUED:
							break;		
					}
				}
				
				if (action) {
					actionBtn.visible = actionBtn.includeInLayout = true;
					actionBtn.label = resourceManager.getString('drilldown', action);
				}
				else {
					actionBtn.visible = actionBtn.includeInLayout = false;
				}
	
				_currentAction = action;
				
			}

			protected function actionBtn_clickHandler(event:MouseEvent):void
			{
				var actionEvent:Event;
				if (_currentAction == DISTRIBUTE) {
					actionEvent = new Event(DistributionActionType.SUBMIT, true);
				}
				else if (_currentAction == RETRY) {
					actionEvent = new Event(DistributionActionType.RETRY, true);
				}
				else if (_currentAction == UPDATE) {
					actionEvent = new Event(DistributionActionType.SUBMIT_UPDATE, true);
				}
				dispatchEvent(actionEvent);
			}

		]]>
	</mx:Script>

	<mx:Button id="actionBtn" styleName="dataGridBtn" click="actionBtn_clickHandler(event)" width="100"/>
</mx:HBox>
