<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute"
				minWidth="955" minHeight="600" creationComplete="creationCompleteHandler(event)" paddingLeft="0"
				paddingRight="0" paddingBottom="0" paddingTop="0" verticalGap="0" backgroundColor="0xFFFFFF" >
	<mx:Metadata>
		[ResourceBundle("bmc")]
	</mx:Metadata>
	<mx:Script>
		<![CDATA[
			import com.borhan.BorhanClient;
			import com.borhan.analytics.GoogleAnalyticsTracker;
			import com.borhan.commands.MultiRequest;
			import com.borhan.commands.permission.PermissionList;
			import com.borhan.commands.session.SessionGet;
			import com.borhan.commands.uiConf.UiConfGet;
			import com.borhan.commands.user.UserGet;
			import com.borhan.commands.userRole.UserRoleGet;
			import com.borhan.config.BorhanConfig;
			import com.borhan.edw.business.permissions.PermissionManager;
			import com.borhan.edw.events.KedErrorEvent;
			import com.borhan.edw.model.datapacks.ContextDataPack;
			import com.borhan.edw.model.types.APIErrorCode;
			import com.borhan.events.BorhanEvent;
			import com.borhan.bmc.business.Helpist;
			import com.borhan.bmc.business.JSGate;
			import com.borhan.bmc.business.BmcModuleLoader;
			import com.borhan.bmc.business.BmcPluginManager;
			import com.borhan.bmc.events.BmcErrorEvent;
			import com.borhan.bmc.events.BmcHelpEvent;
			import com.borhan.bmc.events.BmcModuleEvent;
			import com.borhan.bmc.events.BmcNavigationEvent;
			import com.borhan.bmc.model.BmcModelLocator;
			import com.borhan.bmc.modules.BmcModule;
			import com.borhan.bmc.utils.XMLUtils;
			import com.borhan.bmc.vo.UserVO;
			import com.borhan.bmvc.model.BMvCModel;
			import com.borhan.managers.FileUploadManager;
			import com.borhan.utils.KUtils;
			import com.borhan.utils.ObjectHelpers;
			import com.borhan.vo.BorhanFilterPager;
			import com.borhan.vo.BorhanPermissionListResponse;
			import com.borhan.vo.BorhanSessionInfo;
			import com.borhan.vo.BorhanUiConf;
			import com.borhan.vo.BorhanUser;
			import com.borhan.vo.BorhanUserRole;
			
			import mx.controls.Alert;
			import mx.core.Application;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.events.ResourceEvent;
			import mx.events.StyleEvent;
			import mx.modules.ModuleLoader;
			import mx.resources.IResourceManager;
			import mx.resources.ResourceManager;


			/**
			 * BMC version
			 * */

			public static var VERSION:String = "5.41.2";


			// ==============================================================================
			// members
			// ==============================================================================


			/**
			 * a global access point to the application's flashvars,
			 * all keys are converted to lower case and no underscores.
			 * */
			private var _flashvars:Object;

			/**
			 * application model
			 * */
			private var _modelLocator:BmcModelLocator = BmcModelLocator.getInstance();

			/**
			 * module loader
			 * */
			private var _bmcModuleLoader:BmcModuleLoader;

			/**
			 * BMC UIConf
			 * */
			private var _uiconf:XML;

			/**
			 * Global Context menu
			 * */
			private var _cm:ContextMenu;

			/**
			 * the module BMC should start with, as determined by the url hash
			 * */
			private var _startModule:Object;

			/**
			 * currently showing locale code
			 * */
			protected var _localeCode:String;

			private var _kcTimer:Timer;


			/**
			 * Singletone PermissionManager
			 **/
			public var permissionManager:PermissionManager;

			private var _moduleLoader:ModuleLoader;
			private var _currentObject:Object;

			/**
			 * _bmcFunction.module: module on which we expect to find the following function. <br>
			 * _bmcFunction.funcName: name of function to execute when the requested module is loaded. <br>
			 * _bmcFunction.arg: (optional) a single string argument for the function
			 * */
			private var _bmcFunction:Object;

			/**
			 * plugins are FlexModules which do not function as BmcModules.
			 * they are loaded as simple flex modules and can be refered to
			 * by their id, via the plugins manager.
			 * */
			private var _pluginManager:BmcPluginManager;


			// ==============================================================================
			// methods
			// ==============================================================================

			/**
			 * load skin dynamically
			 * @param path	path to skin .swf file
			 * */
			protected function loadSkin(path:String = ""):void {
				var eventDispatcher:IEventDispatcher = StyleManager.loadStyleDeclarations(path);
				eventDispatcher.addEventListener(StyleEvent.COMPLETE, onSkinLoaded, false, 0, true);
				eventDispatcher.addEventListener(StyleEvent.ERROR, onSkinLoaded, false, 0, true);
			}


			/**
			 * Skin loaded or load error. </br>
			 * We go on anyway, alerting the user if skin load failed.
			 * */
			protected function onSkinLoaded(event:StyleEvent):void {
				if (event.type == StyleEvent.ERROR) {
					alert(event.errorText, ResourceManager.getInstance().getString('bmc', 'skin_load_error_title'));
				}
				
				// title fonts 
				var titleFont:String = resourceManager.getString('bmc', 'title_font'); 
				if (titleFont){
					StyleManager.getStyleDeclaration(".pageTitle2").setStyle("fontFamily", titleFont);
					StyleManager.getStyleDeclaration(".filterTitleHeader").setStyle("fontFamily", titleFont);
				}
				
				// alert buttons width
				var alertButtonWidth:int = resourceManager.getInt('bmc', 'alert_button_width');
				if (alertButtonWidth) {
					Alert.buttonWidth = alertButtonWidth;
				}

				// remove modules that should not be loaded from the BMC uiconf
				var tabsToHide:Array = permissionManager.getRelevantSubTabsToHide(null);
				for each (var moduleName:String in tabsToHide) {
					removeModule(_uiconf, moduleName);
				}

				loadPlugins();
				setHtmlTabs();

				// switch to needed tab
				gotoPage(ExternalInterface.call("bmc.mediator.readUrlHash"));
			}

			protected function loadPlugins():void {
				var handlers:Object = {};
				handlers[BmcNavigationEvent.NAVIGATE] = navigateToModule;
				_pluginManager = new BmcPluginManager(this, _modelLocator.borhanClient, _flashvars, handlers);
				_pluginManager.addEventListener(BmcErrorEvent.ERROR, onError);
				_pluginManager.loadPlugins(_uiconf);
			}





			/**
			 * if any flashvars are supposed to override uiconf values, set them here to the uiconf.
			 * flashvars names will be constructed from module id + "." + xml structure, nodes
			 * separated by ".", i.e, "content.uiconf.metadata" or "admin.locale.language"
			 */
			protected function overrideDataByFlashvars(conf:String, flashvars:Object):String {
				var confFile:XML = new XML(conf);
				// override with general language definition
				if (flashvars.language && !flashvars["bmc.locale.language"]) {
					flashvars["bmc.locale.language"] = flashvars.language;
				}
				// process
				for (var key:String in flashvars) {
					var elements:Array = key.split(".");
					if (elements[0] == 'bmc') {
						// need to process
						// get to the node we need to edit
						var xml:XML = XMLUtils.getElement(confFile, elements);
						if (xml) {
							delete xml.children()[0];
							xml.appendChild(flashvars[key]);
						}
					}
				}
				// re-set values
				return confFile.toXMLString();
			}


			/**
			 * show correct tabs in HTML
			 * */
			protected function setHtmlTabs():void {
				var jstabs:Array = new Array();
				var xmltabs:XMLList = _uiconf.modules.module;
				var jstab:Object;
				var tabKey:String;
				var tabName:String;
				for each (var xmltab:XML in xmltabs) {
					jstab = new Object();
					tabKey = xmltab.@displayName.toString();
					tabName = resourceManager.getString('bmc', tabKey);
					if (!tabName) {
						tabName = tabKey;
					}
					jstab.display_name = tabName;
					jstab.module_name = xmltab.@id.toString();
					jstab.subtab = xmltab.@subtab.toString();

					if (xmltab.@bmcFunction.toString().length > 0) {
						jstab.type = "action";
					}
					else {
						jstab.type = "tab"; 
					}
					if (xmltab.@htmlUrl.toString().length > 0) {
						jstab.html_url = xmltab.@htmlUrl.toString();
					}
					jstabs.push(jstab);
				}
				// call JS function to set html tabs
				JSGate.createTabs(jstabs);
			}


			/**
			 * remove a module from the BMC uiconf
			 * @param moduleId	id of module to remove
			 * */
			protected function removeModule(uiconf:XML, moduleId:String):void {
				if (uiconf.modules.module.(@id == moduleId).length() > 0) {
					delete uiconf.modules.module.(@id == moduleId)[0];
				}
			}
			
			

			/**
			 * process flashvars and load BMC uiconf
			 **/
			protected function creationCompleteHandler(event:FlexEvent):void {
				Security.allowDomain('*');
				systemManager.allowDomain("*");
				_cm = new ContextMenu();
				_cm.hideBuiltInItems();

				var version:ContextMenuItem = new ContextMenuItem("BMC version:" + VERSION);
				_cm.customItems.push(version);

				this.contextMenu = _cm;

				// listen to module/popup/plugin requests
				systemManager.addEventListener(BmcNavigationEvent.NAVIGATE, navigateToModule);
				systemManager.addEventListener(BmcErrorEvent.ERROR, onError);
				systemManager.addEventListener(BmcHelpEvent.HELP, Helpist.showHelp);
				systemManager.addEventListener(/* EntryDetailsWin.HELP_REQUEST */ "helpRequest", Helpist.showHelp);

				// save flashvars (keep original keys as well as lowercased keys)
				var objParam:Object = Application.application.parameters;
				var oLowers:Object = ObjectHelpers.lowerNoUnderscore(objParam);
				for (var key:String in oLowers) {
					objParam[key] = oLowers[key];
				}
				_flashvars = objParam;

				// hook ExternalInterface:
				if (ExternalInterface.available) {
					ExternalInterface.addCallback("gotoPage", gotoPage);
					ExternalInterface.addCallback("alert", alert);
					ExternalInterface.addCallback("hasOngoingProcess", hasOngoingProcess);
					ExternalInterface.addCallback("doHelp", Helpist.showHelp);
				}

				// handle stats:
				if (objParam.disableanalytics || !objParam.urchinnumber) {
					GoogleAnalyticsTracker.getInstance().enabled = false;
				}

				// create BorhanClient:
				var configuration:BorhanConfig = new BorhanConfig();
				configuration.partnerId = objParam.partnerid;
				configuration.ignoreNull = 1;
				configuration.protocol = objParam.protocol ? objParam.protocol : "http://";
				configuration.domain = KUtils.hostFromCode(objParam.host);
				configuration.srvUrl = objParam.srvurl;
				configuration.clientTag = "bmc:" + VERSION;
				configuration.ks = objParam.ks;

				_modelLocator.borhanClient = new BorhanClient(configuration);
				_modelLocator.borhanClient.queueing = true;
				_modelLocator.borhanClient.addEventListener(BorhanClient.CALL_QUEUED, setTimer);
				_modelLocator.borhanClient.addEventListener(BorhanClient.QUEUE_FLUSHED, setTimer);

				var contextData:ContextDataPack = BMvCModel.getInstance().getDataPack(ContextDataPack) as ContextDataPack;
				contextData.sourceUrl = systemManager.loaderInfo.url.substr(0, systemManager.loaderInfo.url.lastIndexOf("/"));
				contextData.protocol = configuration.protocol;

				// create a module loader:
				_bmcModuleLoader = new BmcModuleLoader();
				_bmcModuleLoader.addEventListener(BmcModuleEvent.MODULE_LOAD_ERROR, onModuleLoadError, false, 0, true);
				_bmcModuleLoader.addEventListener(BmcModuleEvent.MODULE_LOADED, onModuleReady, false, 0, true);

				// load BMC uiconf and roles and permission uiConf  
				var mr:MultiRequest = new MultiRequest();

				var uiconf:UiConfGet = new UiConfGet(objParam.bmcuiconf);
				var permissions:UiConfGet = new UiConfGet(objParam.permissionuiconf);

				// get user id from service
				var ssnget:SessionGet = new SessionGet();
				mr.mapMultiRequestParam(3, "userId", 4, "userId");
				//
				// get dummy, the real user id param is passed from the multirequest
				var userget:UserGet = new UserGet('1');
				mr.mapMultiRequestParam(4, "roleIds", 5, "userRoleId");
				// get dummy, the real role id param is passed from the multirequest
				var roleGet:UserRoleGet = new UserRoleGet(1);
				var p:BorhanFilterPager = new BorhanFilterPager();
				p.pageSize = 500;
				var perms:PermissionList = new PermissionList(_modelLocator.permissionsListFilter, p);

				mr.addAction(uiconf);
				mr.addAction(permissions);
				mr.addAction(ssnget);
				mr.addAction(userget);
				mr.addAction(roleGet);
				mr.addAction(perms);

				mr.addEventListener(BorhanEvent.COMPLETE, dataLoadHandler, false, 0, true);
				mr.addEventListener(BorhanEvent.FAILED, dataLoadFailedHandler, false, 0, true);
				_modelLocator.borhanClient.post(mr);
			}


			/**
			 * create the client flushing timer, activate or deactivate it as needed.
			 * */
			private function setTimer(e:Event):void {
				switch (e.type) {
					case BorhanClient.CALL_QUEUED:
						if (!_kcTimer) {
							_kcTimer = new Timer(250);
							_kcTimer.addEventListener(TimerEvent.TIMER, handleTimer, false, 0, true);
						}
						if (!_kcTimer.running) {
							_kcTimer.start();
						}
						break;
					case BorhanClient.QUEUE_FLUSHED:
						if (_kcTimer) {
							_kcTimer.stop();
						}
						break;
				}
			}


			/**
			 * flush client calls.
			 * */
			private function handleTimer(e:TimerEvent):void {
				_modelLocator.borhanClient.flush();
			}


			/**
			 * UiConf failed to load, alert user
			 */
			protected function dataLoadFailedHandler(event:BorhanEvent):void {
				var rm:IResourceManager = ResourceManager.getInstance();
				var s:String = rm.getString('bmc', 'uiconf_load_fail', [event.error.errorMsg]);
				alert(s, rm.getString('bmc', 'error_title'));
			}


			/**
			 * module failed to load, alert user
			 */
			protected function onModuleLoadError(event:BmcModuleEvent):void {
				var rm:IResourceManager = ResourceManager.getInstance();
				var s:String = rm.getString('bmc', 'module_load_fail', [event.errorText]);
				alert(s, rm.getString('bmc', 'error_title'));
				if (progressbar.source == event.moduleLoader) {
					hideProgressBar();
				}
			}


			/**
			 * check if the given object is an error object and respond.
			 * @param  errorObj	server response, object to check
			 * @return true if the given object is a generic error object, false otherwise
			 * */
			protected function isError(errorObj:Object):Boolean {
				if (errorObj) {
					if (errorObj.code == APIErrorCode.INVALID_KS) {
//						alert(errorObj.message, ResourceManager.getInstance().getString('bmc', 'error_title'), Alert.OK, null, expired);
						JSGate.expired();
					}
					else {
						alert(errorObj.message, ResourceManager.getInstance().getString('bmc', 'error_title'));
					}
					return true;
				}
				return false;
			}


			protected function expired(e:CloseEvent):void {
				JSGate.expired();
			}


			/**
			 * Bmc uiConf and RolesAndPermissions loaded. Parse it and load skin and relevant modules.
			 */
			protected function dataLoadHandler(event:BorhanEvent):void {
				// [0] BMC uiconf:
				if (isError(event.data[0].error))
					return;

				_uiconf = new XML(overrideDataByFlashvars((event.data[0] as BorhanUiConf).confFile, _flashvars));
				if (_uiconf.descendants().length() == 0) {
					Alert.show("BMC UIConf is empty", "Error");
					return;
				}
				// help url
				var helpBaseUrl:String = _uiconf.help.helpPage.text().toString();
				Helpist.init(_uiconf.help.item, helpBaseUrl, _flashvars.host, _flashvars.protocol);

				// [1] permissions:
				if (isError(event.data[1].error))
					return;
				var uidefinition:XML = new XML((event.data[1] as BorhanUiConf).confFile); // all permissions
				// [2] SessionInfo (only used for getting user id, which is already used in the user.get call)
				if (isError(event.data[2].error)) {
					BorhanSessionInfo;
					return;
				}
				// [3, 4] user and role:
				if (isError(event.data[3].error))
					return;
				if (isError(event.data[4].error))
					return;
				var uvo:UserVO = new UserVO();
				uvo.user = event.data[3] as BorhanUser;
				uvo.role = event.data[4] as BorhanUserRole;
				_modelLocator.userInfo = uvo;

				var roleOfCurrentUser:String;
				roleOfCurrentUser = uvo.role.permissionNames;

				// [5] partner permissions list
				if (isError(event.data[5].error))
					return;

				permissionManager = PermissionManager.getInstance();
				permissionManager.addEventListener(KedErrorEvent.ERROR, onError);
				permissionManager.init(uidefinition, roleOfCurrentUser, event.data[5] as BorhanPermissionListResponse);

				// GAtracking
				var ga:GoogleAnalyticsTracker = GoogleAnalyticsTracker.getInstance();
				if (ga.enabled) {
					// only if we didn't disable before..
					ga.init(_modelLocator.userInfo.user.partnerId.toString(), _modelLocator.userInfo.user.id, this, "BMC", _flashvars.urchinnumber, "AS3", _flashvars.gaDebug == "true" ? true : false);
				}

				// load locale
				loadLocale(getUrl(_uiconf.locale.path.toString()), _uiconf.locale.language.toString());

				// file uploader
				FileUploadManager.getInstance().kc = _modelLocator.borhanClient;
				FileUploadManager.getInstance().debugMode = _flashvars.debugmode;
				if (!_flashvars.maxuploadsize) {
					_flashvars.maxuploadsize = parseFloat(_uiconf.upload.maxSize.toString());
					_flashvars.maxUploadSize = _flashvars.maxuploadsize;
				} 
				// get the concurrentuploads param from bmc uiconf 
				FileUploadManager.getInstance().concurrentUploads = parseInt(_uiconf.upload.concurrent.toString());

			}


			/**
			 * Load locale data.
			 * @param localePath	path to the locale (.swf) file
			 * @param language		locale code (i.e. en_US)
			 * */
			protected function loadLocale(localePath:String, language:String):void {
				_localeCode = language;
				localePath = localePath.replace(/{locale}/g, language);
				var eventDispatcher:IEventDispatcher = ResourceManager.getInstance().loadResourceModule(localePath, true);
				eventDispatcher.addEventListener(ResourceEvent.ERROR, localeLoadCompleteHandler);
				eventDispatcher.addEventListener(ResourceEvent.COMPLETE, localeLoadCompleteHandler);
			}


			/**
			 * Set use of loaded locale.
			 * This is also the place to update any values which are not
			 * bound to resource manager values and have to be set manualy.
			 * */
			protected function localeLoadCompleteHandler(event:ResourceEvent):void {
				event.target.removeEventListener(ResourceEvent.COMPLETE, localeLoadCompleteHandler);
				event.target.removeEventListener(ResourceEvent.ERROR, localeLoadCompleteHandler);
				if (event.type == ResourceEvent.ERROR) {
					Alert.show(event.errorText, "Locale Error", Alert.OK);
				}
				var chain:Array;
//				if (_localeCode == "en_US") {
//					chain = [_localeCode];
//				}
//				else {
				chain = [_localeCode, "en_US"];
//				}
				ResourceManager.getInstance().localeChain = chain;

				// load skin
				loadSkin(getUrl(_uiconf.skinPath.toString()));
			}


			/**
			 * initialize module: set its size and id, trigger its init method
			 * and switch to its needed subtab.
			 * */
			protected function onModuleReady(event:BmcModuleEvent):void {
				// use the moduleLoader to get module load identifier
				var ml:ModuleLoader = event.moduleLoader;
				var moduleid:String = _bmcModuleLoader.getModuleLoadId(ml);

				// get the loaded module
				var mod:BmcModule = ml.child as BmcModule;

				// fix size:
				mod.percentHeight = 100;
				mod.percentWidth = 100;
				mod.id = mod.getModuleName();

				// get the uiconf id this module should load:
				var uiconfid:String = _uiconf.modules.module.(@id == moduleid)[0].@uiconf;

				// init:
				mod.init(_modelLocator.borhanClient, uiconfid, _flashvars, _modelLocator.userInfo, _cm);

				// if we need to start with a module, see if it's this one
				if (_startModule && _startModule.moduleName == mod.id) {
					if (_startModule.subtab) {
						mod.showSubtab(_startModule.subtab, _startModule.extra);
					}
					_startModule = null;
				}
				if (progressbar.source == ml) {
					hideProgressBar();
				}
			}


			/**
			 * get the default page for this user according to their permissions:
			 * the first module in the BMCUiconf that is not hidden.
			 * @return 	an object {moduleName:String}, where moduleName is an id of
			 * 			a module this user is allowed to see.
			 * */
			protected function getDefaultPage():Object {
				var o:Object = {};
				var modules:XMLList = _uiconf.modules.module;
				var moduleid:String;
				var hideModule:Boolean;
				for each (var module:XML in modules) {
					moduleid = module.@id.toString();
					for (var s:String in permissionManager.hideTabs) {
						if (moduleid == s) {
							// this module is hidden
							hideModule = true;
							break;
						}
					}
					if (!hideModule) {
						// return this module name
						o.moduleName = moduleid;
						o.subtab = module.@subtab.toString();
						break;
					}
				}
				return o;
			}


			/**
			 * decide if should use relative or absolute url.
			 * if the given path is ablsolute, return the same string.
			 * if the given path is relative, concatenate it to the swf url.
			 * @param	given path
			 * @return	path to use
			 * */
			protected function getUrl(path:String):String {
				var url:String;
				if (path.indexOf("http") == 0) {
					url = path;
				}
				else {
					var base:String = loaderInfo.url.substr(0, loaderInfo.url.lastIndexOf("/"));
					url = base + "/" + path;
				}
				return url;
			}


			/**
			 * Switch to a different BMC module (tab).
			 * Can also include subtab on the new module.
			 * This method can be triggered from JS using ExternalInterface.
			 * @param o		{moduleName:String, subtab:String, extra:Object}
			 * */
			protected function gotoPage(o:Object):void {
				var mInfo:XML;
				_currentObject = o;
				// create navigation info object:
				// ------------------------------
				if (o) {
					mInfo = _uiconf.modules.module.(@id == o.moduleName)[0];
				}
				else {
					// take the first on the BMC uiconf
					mInfo = _uiconf.modules.module[0];
					o = {moduleName: mInfo.@id.toString(), subtab: mInfo.@subtab.toString()};
					_currentObject = o;
				}
				if (!mInfo) {
					var s:String = ResourceManager.getInstance().getString('bmc', 'module_unavailable', [o.moduleName]);
//					dispatchEvent(new BmcErrorEvent(BmcErrorEvent.ERROR, s));
					trace(s);
					gotoPage(getDefaultPage());
					return;
				}

				// if no subtab supplied, use the one listed in the uiconf
				if (!o.subtab || o.subtab == "") {
					o.subtab = mInfo.@subtab.toString();
				}
				if (!o.extra) {
					o.extra = new Object();
				}
				// if no html tab, use the one from uiconf
				if (!o.extra.htmlUrl) {
					o.extra.htmlUrl = mInfo.@htmlUrl.toString();
				}
				// if no bmc function, use the one from uiconf
				if (!o.extra.bmcFunction) {
					o.extra.bmcFunction = mInfo.@bmcFunction.toString();
				}

				// actual navigation part:
				// -----------------------
				// check for HTML tabs
				if (o.extra.htmlUrl) {
					if (o.extra.htmlUrl.indexOf('ei:') == 0) {
						// trigger the rest of the string using ExternalInterface
						JSGate.triggerJS(o.extra.htmlUrl.substring(3));
					}
					else {
						// tell JS it should show a html tab
						JSGate.openIframe(o.extra.htmlUrl);
					}
					// tell html
					JSGate.writeUrlHash(o.moduleName, o.subtab);
					JSGate.setTab(o.moduleName, true);
				}
				// check for action tabs (bmcFunction="module_id.function_name")
				else if (o.extra.bmcFunction) {
					handleBmcFunction(o);
					// tell html
//					JSGate.setTab(o.moduleName, false);
					return;
				}
				else {
					// load BMC module
					loadModule(mInfo);
				}
				// tell app
				dispatchEvent(new BmcNavigationEvent(BmcNavigationEvent.NAVIGATION_COMPLETE, o.moduleName, o.subtab)); 
			}

			
			/**
			 * input: [module.]funcName[(singleStringArg)]
			 * output: {module, funcName, arg}
			 * */
			private function parseBmcFunction(func:String):Object {
				var regex:RegExp = /((\w+)\.)?(\w+)(\((\w*)\))?/g ;
				var result:Object = regex.exec(func);
				var o:Object = {};
				o.module = result[2] ? result[2] : null;
				o.funcName = result[3] ? result[3] : null;
				o.arg = result[5] ? result[5] : null;
				return o;
			}

			/**
			 * execute given function either on BMC itself, a module or a plugin
			 * */
			private function handleBmcFunction(moduleInfo:Object):void {
				
				var o:Object = parseBmcFunction(moduleInfo.extra.bmcFunction);
				
				// triggering BMC method
				if (!o.module) {
					if (o.arg) {
						this[o.funcName](o.arg);
					}
					else {
						this[o.funcName]();
					}
				}
				// triggering ready module method
				else if (getModuleParent(o.module) && (getModuleParent(o.module).child as BmcModule).isModuleReady) {
					try {
						if (o.arg) {
							getModuleParent(o.module).child[o.funcName](o.arg);
						}
						else {
							getModuleParent(o.module).child[o.funcName]();
						}
					}
					catch (e:Error) {
						alert(resourceManager.getString('bmc', 'method_dont_exist', [o.module, o.funcName]), resourceManager.getString('bmc', 'error_title'));
						trace("error: ", e.name);
						trace("error: ", e.errorID, e.message);
						trace(e.getStackTrace());
						trace('-----------------------------------');
					}
				}
				// either unloaded module or a plugin
				else {
					var mInfo:XML = _uiconf.modules.module.(@id == o.module)[0];
					if (mInfo) {
						if (moduleInfo.extra.forceLoad == "false") {
							// this means we don't load a module, the function will not be triggered at all.
							return;
						}
						// save func_name
						_bmcFunction = o;
						// add listener to moduleReady event
						addEventListener(BmcModule.MODULE_READY, handleSavedBmcFunction, false, 0, true);
						// start loading the relevant module
						_currentObject = {moduleName: o.module, subtab: mInfo.@subtab.toString()};
						loadModule(mInfo);
						
					}
					// no module, try triggering plugin method
					else {
						_pluginManager.executePluginMethod(o.module, o.funcName);
							// this is our last option, and if it fails a BmcErrorEvent is dispatched
							// so basically, wer'e good.
					}
				}
			}

			/**
			 * if a bmcFunction is waiting to execute for the ready module, execute it.
			 * */
			private function handleSavedBmcFunction(e:Event):void {
				if (_bmcFunction && (e.target as BmcModule).getModuleName() == _bmcFunction.module) {
					removeEventListener(BmcModule.MODULE_READY, handleSavedBmcFunction, false);
					// try to trigger func_name on relevant module
					try {
						if (_bmcFunction.arg) {
							(e.target as BmcModule)[_bmcFunction.funcName](_bmcFunction.arg);
						}
						else {
							(e.target as BmcModule)[_bmcFunction.funcName]();
						}
					}
					catch (er:Error) {
						alert(resourceManager.getString('bmc', 'method_dont_exist', [_bmcFunction.module, _bmcFunction.funcName]), resourceManager.getString('bmc', 'error_title'));
						trace("error: ", er.name);
						trace("error: ", er.errorID, er.message);
						trace(er.getStackTrace());
						trace('-----------------------------------');
					}
					_bmcFunction = null;
				}
			}

			/**
			 * load BMC module
			 * @param mInfo		module info from uiconf
			 * */
			private function loadModule(mInfo:XML):void {
				var url:String = getUrl(mInfo.@path.toString());
				_moduleLoader = _bmcModuleLoader.loadBmcModule(url, mInfo.@id);
				if (!_moduleLoader.parent) {
					// this is the first time we load this module.
					// add to the viewstack if needed
					mainViewStack.addChild(_moduleLoader);
					// show preloader, set its source
					progressbar.source = _moduleLoader;
					progressbar.visible = true;
				}

				if ((mainViewStack.selectedChild) && (mainViewStack.selectedChild as ModuleLoader).child) {
					this.addEventListener(BmcModule.FINISHED_SAVING, onModuleReadyToNavigate);
					((mainViewStack.selectedChild as ModuleLoader).child as BmcModule).checkForSaveSubTab();
				}
				else {
					changeSelectedTab();
				}
			}


			private function changeSelectedTab():void {
				mainViewStack.selectedChild = _moduleLoader;
				_startModule = _currentObject;
				JSGate.showFlash();
				if (_moduleLoader.child && _currentObject.subtab) {
					(_moduleLoader.child as BmcModule).showSubtab(_currentObject.subtab, _currentObject.extra);
				}
				// tell html
				JSGate.writeUrlHash(_currentObject.moduleName, _currentObject.subtab);
				JSGate.setTab(_currentObject.moduleName, true);
			}


			private function onModuleReadyToNavigate(event:Event):void {
				this.removeEventListener(BmcModule.FINISHED_SAVING, onModuleReadyToNavigate);
				changeSelectedTab();
			}


			/**
			 * get the ModuleLoader instance that holds the required module.
			 * @internal mainViewstack holds ModuleLoader instances, not modules.
			 * @param	name (id) of desired module.
			 * @return 	ml holding the given module.
			 * */
			protected function getModuleParent(moduleName:String):ModuleLoader {
				var children:int = mainViewStack.numChildren;
				var ml:ModuleLoader;
				for (var i:int = 0; i < children; i++) {
					ml = mainViewStack.getChildAt(i) as ModuleLoader;
					if (ml.child && (ml.child as BmcModule).getModuleName() == moduleName) {
						return ml;
					}
				}
				return null;

			}


			protected function hideProgressBar():void {
				progressbar.visible = false;
				progressbar.source = null;
			}


			/**
			 * Trigger module switching, triggerd in response to <code>BmcNavigationEvent.NAVIGATE</code>
			 * @param e		event holding navigation request info.
			 * */
			protected function navigateToModule(e:BmcNavigationEvent):void {
				e.stopImmediatePropagation();
				var o:Object = {moduleName: e.module, subtab: e.subtab, extra: e.data};
				gotoPage(o);
			}


			/**
			 * Show an error with the text on the error event.
			 * */
			protected function onPermissionError(e:KedErrorEvent):void {
				e.stopImmediatePropagation();
				alert(e.error, ResourceManager.getInstance().getString('bmc', 'error_title'));
			}

			/**
			 * Show an error with the text on the error event.
			 * This text should already be localized by the module
			 * which dispatched this event.
			 * */
			protected function onError(e:Event):void {
				e.stopImmediatePropagation();
				var error:String;
				if (e is BmcErrorEvent) {
					error = (e as BmcErrorEvent).error;
				}
				else if (e is KedErrorEvent) {
					error = (e as KedErrorEvent).error;
				}
				alert(error, ResourceManager.getInstance().getString('bmc', 'error_title'));
			}


			/**
			 * show an alert with the default "ok" button.
			 * @param
			 * */
			protected function alert(text:String, header:String = "", flags:uint = 4, parent:Sprite = null, closeHandler:Function = null):void {
				Alert.show(text, header, flags, parent, closeHandler);
			}


			/**
			 * check if any processes are going on that will
			 * stop upon navigating away from the BMC.
			 * */
			protected function hasOngoingProcess():String {
				var result:String;
				if (FileUploadManager.getInstance().getAllFiles().length > 0) {
					result = resourceManager.getString('bmc', 'ongoing_uploads');
				}
				return result;
			}




		]]>
	</mx:Script>
	<mx:ViewStack id="mainViewStack" width="100%" height="100%"/>
	<mx:ProgressBar id="progressbar" y="{(height - progressbar.height) / 2}" x="{(width - progressbar.width) / 2}"/>
</mx:Application>
