<?xml version="1.0" encoding="utf-8"?>
<!---
	 This panel allows changing access control for entries
-->
<HelpTitleWindow xmlns="com.borhan.containers.*" xmlns:mx="http://www.adobe.com/2006/mxml" 
				 title="{resourceManager.getString('cms', 'setAccessControlTitle')}" layout="vertical"
				 showCloseButton="true" close="closeWindow(event)" help="onHelp(event)" 
				 xmlns:view="com.borhan.edw.view.*">
	<mx:Script>
		<![CDATA[
			import com.borhan.analytics.GoogleAnalyticsConsts;
			import com.borhan.analytics.GoogleAnalyticsTracker;
			import com.borhan.analytics.KAnalyticsTracker;
			import com.borhan.analytics.KAnalyticsTrackerConsts;
			import com.borhan.edw.business.Cloner;
			import com.borhan.edw.business.IDrilldownPanel;
			import com.borhan.edw.constants.PanelConsts;
			import com.borhan.edw.model.FilterModel;
			import com.borhan.edw.model.datapacks.ContextDataPack;
			import com.borhan.edw.model.datapacks.FilterDataPack;
			import com.borhan.edw.model.util.CompositeKBaseEntry;
			import com.borhan.edw.model.util.EntryAccessControlUtil;
			import com.borhan.edw.view.panels.PanelLoader;
			import com.borhan.edw.view.panels.PanelMetadataParser;
			import com.borhan.edw.view.panels.PanelMetadataVO;
			import com.borhan.bmc.events.BmcHelpEvent;
			import com.borhan.bmc.modules.content.events.EntriesEvent;
			import com.borhan.bmc.modules.content.events.WindowEvent;
			import com.borhan.bmvc.model.IDataPackRepository;
			import com.borhan.types.BorhanStatsBmcEventType;
			import com.borhan.vo.BorhanBaseEntry;
			
			import mx.collections.ArrayCollection;
			import mx.events.FlexEvent;


			private var _selectedEntries:ArrayCollection;
			
			[Bindable]
			private var _clonedSelectedEntries:ArrayCollection = new ArrayCollection();

			[Bindable]
			public var filterModel:FilterModel;
			
			public var entryDetailsModel:IDataPackRepository;
			
			private var _entryACP:IDrilldownPanel;
			
			/**
			 * application context
			 * */
			public var context:ContextDataPack;
			
			
			public function set selectedEntries(arr:ArrayCollection):void {
				_selectedEntries = arr;
				var tempArrCol:ArrayCollection = new ArrayCollection();
				for (var i:int = 0; i < _selectedEntries.length; i++) {
					tempArrCol.addItem(Cloner.cloneBorhanBaseEntry(_selectedEntries[i]));
				}

				_clonedSelectedEntries = tempArrCol;
			}
			
			override protected function createChildren():void{
				super.createChildren();
				
				// if Panel Loader not yet initialised
				try {
					PanelLoader.instance;
				} 
				catch (e:IllegalOperationError) {
					var panelDataVO:PanelMetadataVO = PanelMetadataParser.parse(context.panelsConfig);
					PanelLoader.initialize(panelDataVO, context.sourceUrl);
				}
				
				PanelLoader.instance.createPanel(PanelConsts.ACCESS_CONTROL_PANEL, onPanelCreated);
			}


			private function closeWindow(event:Event = null):void {
				var cgEvent:WindowEvent = new WindowEvent(WindowEvent.CLOSE);
				cgEvent.dispatch();
			}


			private function saveData(event:Event):void {
				if (_entryACP != null){
					var cgEvent:EntriesEvent = new EntriesEvent(EntriesEvent.UPDATE_ENTRIES, _clonedSelectedEntries, false);
					cgEvent.dispatch();
	
					for each (var etr:Object in _clonedSelectedEntries) {
						KAnalyticsTracker.getInstance().sendEvent(KAnalyticsTrackerConsts.CONTENT,BorhanStatsBmcEventType.CONTENT_EDIT_ENTRY,
																  "setAccessControlProfileIdForEntries", etr.id);
	
					}
					GoogleAnalyticsTracker.getInstance().sendToGA(GoogleAnalyticsConsts.CONTENT_ACCESS_CONTROL,GoogleAnalyticsConsts.CONTENT);
				}
				closeWindow();
			}

			protected function onHelp(event:Event):void {
				dispatchEvent(new BmcHelpEvent(BmcHelpEvent.HELP, 'section_bulk_access_control'));
			}

			private function onPanelCreated(panel:IDrilldownPanel):void{
				_entryACP = panel;
				addChildAt(_entryACP as DisplayObject, 1);
				
				addEventListener(FlexEvent.UPDATE_COMPLETE, onUpdateComplete);
			}
			
			private function onUpdateComplete(event:FlexEvent):void
			{
				removeEventListener(FlexEvent.UPDATE_COMPLETE, onUpdateComplete);
				_entryACP.dataPackModel = entryDetailsModel;
				EntryAccessControlUtil.setModel(filterModel);
				var entryVect:Vector.<BorhanBaseEntry> = Vector.<BorhanBaseEntry>(_clonedSelectedEntries.source);
				var composite:CompositeKBaseEntry = new CompositeKBaseEntry(entryVect);
				_entryACP.selectedEntry = composite;
			}
			
		]]>
	</mx:Script>
	<mx:Label text="{resourceManager.getString('cms', 'accessControlOverrideMsg')}"/>
	<mx:ControlBar width="100%" horizontalAlign="center" paddingBottom="2" paddingLeft="2" paddingRight="2"
				   paddingTop="2">
		<mx:Button label="{resourceManager.getString('cms', 'applyAccessControlBtn')}" useHandCursor="true"
				   buttonMode="true" click="saveData(event)"/>
	</mx:ControlBar>
</HelpTitleWindow>
